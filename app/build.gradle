/*
 * Copyright (c) 2019 Frederik Kammel <vatbub123@googlemail.com>
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.ajoberstar.grgit.Grgit

plugins {
    id 'com.mikepenz.aboutlibraries.plugin'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

ext {
    git = Grgit.open(dir: file('..'))
    GIT_COMMIT_COUNT = git.log(includes: ['HEAD']).size()
    GIT_COMMIT_HASH = git.head().abbreviatedId
    GIT_COMMIT_HASH_LONG = git.head().id
}

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "com.github.vatbub.scoreboard"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 248
        versionName "1.0-248"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "GitHash", "\"$GIT_COMMIT_HASH\""
        buildConfigField "String", "GitFullHash", "\"$GIT_COMMIT_HASH_LONG\""
    }
    signingConfigs {
        release {
            keyAlias 'key0'
            keyPassword System.getenv('keypass')
            storeFile rootProject.file("keystore/buddyci.jks")
            storePassword System.getenv('storepass')
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lint {
        abortOnError false
    }
    namespace 'com.github.vatbub.scoreboard'
}

dependencies {
    implementation 'net.steamcrafted:materialiconlib:1.1.5'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'org.robolectric:robolectric:4.9.2'
    testImplementation 'com.google.truth:truth:1.1.3'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.jdom:jdom2:2.0.6.1'
    implementation 'ru.noties:markwon:2.0.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "com.mikepenz:aboutlibraries-core:${latestAboutLibsRelease}"
    implementation "com.mikepenz:aboutlibraries:${latestAboutLibsRelease}"
}

task("bumpVersionCode") {
    doLast {
        File scriptFileLocation = buildscript.sourceFile
        println("Bumping versionCode in ${scriptFileLocation.absolutePath}")
        List<String> scriptContents = scriptFileLocation.readLines()
        int currentVersionCode = scriptContents
                .find { it.contains("versionCode") }
                .trim()
                .split(" ")
                .last()
                .toInteger()
        int newVersionCode = currentVersionCode + 1

        println("The current version code is $currentVersionCode. The new version code will be $newVersionCode.")

        String newScriptContents = scriptContents.stream()
                .map {
                    if (it.contains("versionCode") || it.contains("versionName"))
                        it.replace(currentVersionCode.toString(), newVersionCode.toString())
                    else it
                }
                .toList()
                .join("\n")

        scriptFileLocation.write(newScriptContents)
    }
}